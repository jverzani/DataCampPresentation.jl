{
  "nbformat": 4,
  "nbformat_minor": 0,
  "metadata": {
    "accelerator": "GPU",
    "colab": {
      "name": "Julia_1.8.2_template.ipynb",
      "provenance": [],
      "collapsed_sections": [],
      "include_colab_link": true
    },
    "kernelspec": {
      "display_name": "Julia 1.8",
      "language": "julia",
      "name": "julia-1.8"
    },
    "language_info": {
      "file_extension": ".jl",
      "mimetype": "application/julia",
      "name": "julia",
      "version": "1.8.2"
    }
  },
  "cells": [
    {
      "cell_type": "markdown",
      "metadata": {
        "id": "view-in-github",
        "colab_type": "text"
      },
      "source": [
        "<a href=\"https://colab.research.google.com/github/jverzani/DataCampPresentation.jl/blob/main/datacamp.ipynb\" target=\"_parent\"><img src=\"https://colab.research.google.com/assets/colab-badge.svg\" alt=\"Open In Colab\"/></a><a href=\"https://mybinder.org/v2/gh/jverzani/DataCampPresentation.jl/main?labpath&#61;datacamp.ipynb\"><img src=\"https://mybinder.org/badge_logo.svg\" alt=\"Binder\" /></a>"
      ]
    },
    {
      "cell_type": "code",
      "metadata": {
        "id": "PMGwZ7aFJL8Y"
      },
      "source": [
        "# Installation cell\n",
        "%%capture\n",
        "%%shell\n",
        "if ! command -v julia 3>&1 > /dev/null\n",
        "then\n",
        "    wget -q 'https://julialang-s3.julialang.org/bin/linux/x64/1.8/julia-1.8.2-linux-x86_64.tar.gz' \\\n",
        "        -O /tmp/julia.tar.gz\n",
        "    tar -x -f /tmp/julia.tar.gz -C /usr/local --strip-components 1\n",
        "    rm /tmp/julia.tar.gz\n",
        "fi\n",
        "julia -e 'using Pkg; pkg\"add IJulia PlotlyLight DataFrames CSV Chain FreqTables CategoricalArrays; precompile;\"'\n",
        "echo 'Done'"
      ],
      "execution_count": 1,
      "outputs": []
    },
    {
      "cell_type": "markdown",
      "metadata": {
        "id": "XdMpcQduyaQc"
      },
      "source": [
        "After you run the first cell (the the cell directly above this text), go to Colab's menu bar and select **Edit** and select **Notebook settings** from the drop down. Select *Julia 1.8* in Runtime type. You can also select your prefered harwdware acceleration (defaults to GPU). \n",
        "\n",
        "<br/>You should see something like this:\n",
        "\n",
        "> ![Colab Img](https://raw.githubusercontent.com/Dsantra92/Julia-on-Colab/master/misc/julia_menu.png)\n",
        "\n",
        "<br/>Click on SAVE\n",
        "<br/>**We are ready to get going**\n",
        "\n",
        "\n",
        "\n"
      ]
    },
    {
      "cell_type": "code",
      "metadata": {
        "id": "iIxu4TjlJnBG",
        "colab": {
          "base_uri": "https://localhost:8080/"
        },
        "outputId": "ddf02770-daba-47a0-e2bf-9947734b3ea5"
      },
      "source": [
        "VERSION"
      ],
      "execution_count": 1,
      "outputs": [
        {
          "output_type": "execute_result",
          "data": {
            "text/plain": [
              "v\"1.7.2\""
            ]
          },
          "metadata": {},
          "execution_count": 1
        }
      ]
    },
{"cell_type":"markdown","source":"# Using Julia for some data wrangling taks\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"`Julia` is a scripting language, like R and Python, with similarities and differences:\n","metadata":{}},
{"cell_type":"markdown","source":"  * Like R and Python, the basic syntax of `Julia` is easy enough to learn\n  * Like R and unlike Python, `Julia` has a concept of generic methods and multiple dispatch as an alternative to object-oriented approaches\n  * `Julia` has a richer type system with parametric types helpful for generic programming over concrete types.\n  * `Julia`'s multiple dispatch is said to be as easy as S3 and as powerful as S4 (Styles for R)\n  * Unlike R and Python (but not variants), `Julia` uses llvm to on-the-fly compile its methods, resulting in a tradeoff balancing compile-time latency on first use with very rapid runtime after compilation\n  * When well written, `Julia` can match speeds of C and Fortran, so it is possible to avoid the \"two-language\" problem\n  * Like R and Python, base `Julia` code is readily extended  by add-on packages; packages are easily managed by a package manager.\n  * Like `R` and `Python`, `Julia` readily interfaces with other languages  (R, Python, C are good examples)\n  * `Julia` inherits practices from: lisp, R, Python, Ruby, and Matlab (making anyone feel at home?)\n  * For numerical programming, as is often done in Matlab and Python, `Julia` has many best in class packages (e.g. `SciML`)\n  * For general purpose programming, such is often done with Python, `Julia` has a pretty rich set of packages\n  * For statistical programming, such is often done with R, `Julia` has many packages and great promise for new-package development. Unlike R, most all statistical features are in add-on packages, such as `DataFrames.jl` shown below.\n","metadata":{}},
{"cell_type":"markdown","source":"This presentation will demonstrate a modest data wrangling task that might be familiar to R users or Pandas users.\n","metadata":{}},
{"cell_type":"markdown","source":"## A few key Julia features\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"### Types\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Julia has types (not classes) for different code values\n","metadata":{}},
{"cell_type":"markdown","source":"Base number types include:\n","metadata":{}},
{"cell_type":"markdown","source":"  * integer, float, rational, big numbers, complex\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["i,s,r,b,c = 1, 1.0, 1//1, big(1), 1 + 0im"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["typeof(i), typeof(r), typeof(s), typeof(b), typeof(c)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"  * concrete versus abstract (Real, Integer, AbstractFloat)\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["isa(i, Integer), isa(r, Integer), isa(b, Integer)"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["isa(s, Real), isa(s, AbstractFloat)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Strings and symbols play a role in names for data frames:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["\"string\", :symbol"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"`Julia` has both `nothing` and `missing`, with `missing` playing the role of `NA` in R.\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["nothing, missing"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"### Containers\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Base `Julia` provides many basic containers for values:\n","metadata":{}},
{"cell_type":"markdown","source":"Vectors promote values to a common type\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["[1, 1.0, 1//1]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Tuples allow for heterogeneous containers (like a list in R)\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["(1, 1.0, 1//1)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"While in R, vectors are matrices, in `Julia` they are distinct, though both are special cases of the `Array{T, N}` type:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["v = [1,2,3]"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["typeof(v)"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["M = [1 2; 3 4]"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["typeof(M)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The matrix `M`, as defined above, is loaded row by row. We can create a row vector, mirroring `v`, but it is an array with 2 coordinates:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["vr = [1 2 3]"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["typeof(vr)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Julia takes transposes seriously (i.e. `v'` is not `vr`):\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["v'"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Associative arrays are implemented by dictionaries or named tuples:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["nt = (a=1, b=2, c=3)\nnt.a"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["dct = Dict(\"a\"=>1, \"b\"=>2, \"c\"=>3)\ndct[\"a\"]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"(The keys of a named tuple are symbols, for the dictionary they are strings above, but may be other types.)\n","metadata":{}},
{"cell_type":"markdown","source":"In Julia it is very much possible for external packages to provide additional container types. We will use `DataFrame` and `NamedTable` in the following.\n","metadata":{}},
{"cell_type":"markdown","source":"### Functions\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"There are different ways to define a function and different types of functions. These define two methods for a generic function `f`:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f(x) = x^5 + x - 1"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["function f(x, y)\n   x + 2x*y + y^2\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"This defines an anonymous function which is then *assigned* to `g`:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["g = x -> x^5 - x - 1"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Both types of functions are called in the conventional way:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f(1), g(1)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"But generic functions have dispatch determined by the signature. Here we see the number of arguments dictates which method is called:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f(1,2) # uses f(x,y) not a call to f(x), which would error"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Dispatch on the type of an argument is possible too, and perhaps more common. Here are default methods for `log` restricted to an initial argument of type `Number`:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["methods(log, (Number,))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Packages and users can extend the `log` generic for other types, though it is *expected* that it be narrowed to types that they \"own.\"\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"Anonymous functions are useful with higher order programming. The above definition for `g` is just a binding of the name to the anonymous function. Bindings are dynamic:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["g = 9.8"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The generic function adds to a (global) method table. The method table can be modified, but the binding can not:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f(x,y,z) = x^2 + y^2 + x^2"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["#| error: true\nf = 42"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Functions may have *positional* arguments (possibly with default values) and *keyword* ararguments (with default values)\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["h(a, b, c=3; d=4, e::Integer=5) = (a,b,c,d,e)\n@show h(1, 2)\n@show h(1, 2, 4)\n@show h(1, 2; d=6)  # ; or , are okay when calling h; positional first"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Function application is also available through the `|>` (pipe) operator:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["3 |> f  # calls f(x) = x^5 - x - 1 with a value of `3`"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The definition for this operator is just\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["#| eval: false (will error, it extends a base operator...)\n|>(x, f) = f(x)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"## The dot syntax\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"R is vectorized. Matlab is also, but needs a \"dot\" to disambiguate certain operations. Current `Julia` is not vectorized, but a \"dot\" is used to broadcast function calls over the arguments (perhaps of different sizes)\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["x = [1,2,3]\nf.(x)  # [f(1), f(2), f(3)] like `map(f, x)`"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["a = [:a, :b]  # \"column vector\"\nb = [:c :d]   # row vector\nh(x,y) = (x,y)\nh.(a,b)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"## Iteration\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Basic iteration can be done using a for loop:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["a, b = 1, 1\nfor i in 1:3\n    a, b = b, a + b\nend\na, b"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Many objects are iterable (as the range `1:3` above). There are several *helper* functions for iteration. Among others, these examples show 3 ways to iterate over a matrix (each element, each row, each column):\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["M = [1 2; 3 4]\nfor r âˆˆ M # order of traversal down column then over row\n    @show r\nend"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["for r âˆˆ eachrow(M)\n    @show r\nend"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["for r âˆˆ eachcol(M)\n    @show r\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Basic iteration tasks can also be achieved with a comprehension:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["[2i + 4 for i in 1:3]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Similar to map which takes a function rather than an expression:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["map(i -> 2i + 4, 1:3)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Base `Julia` provides many other higher order functions and add-on packages even more.\n","metadata":{}},
{"cell_type":"markdown","source":"## A data wrangling example using DataFrames\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Base Julia is extended by add-on packages. The built-in package manager can install them.\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["#| eval: false\nusing Pkg\nPkg.add([\"CSV\", \"DataFrames\"])"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Packages are included in a session via `using` (or `import`):\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["using CSV, DataFrames"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The background for this data is a possible college, Euphoria State. Each semester student records are stored, in this example as CSV files. These records included many things, but in particular grade for each class by student. The scenario below is there are historic grades and new enrollment data in separate files.\n","metadata":{}},
{"cell_type":"markdown","source":"The `CSV` provides a package for parsing structured data into a tabular format.  Generally the data is in a file, here we read it in from a multi-line string and store the data as a `DataFrame` object:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["s11_data = \"\"\"\nColumn1,Term,Subject,Catalog,ID,Name,Session,Grade,Grade.In\n1,1149,ACC,114,812315,\"Abernathy,Alice\",1,A,A\n2,1149,MTH,123,812315,\"Abernathy,Alice\",1,C,C\n3,1152,ENG,132,812315,\"Abernathy,Alice\",1,A,A\n4,1152,ENG,211,812315,\"Abernathy,Alice\",1,B,B\n5,1169,MTH,231,889995,\"Ballew,Bob\",1,A,A\n6,1169,MTH,229,889995,\"Ballew,Bob\",1,A,A\n7,1172,ENG,111,889995,\"Ballew,Bob\",1,B,B\n8,1172,CSC,222,889995,\"Ballew,Bob\",1,A-,A-\n9,1179,CSC,222,889995,\"Ballew,Bob\",1,F,F\n10,1179,ENG,232,889995,\"Ballew,Bob\",1,A,A\n11,1182,PSY,100,889995,\"Ballew,Bob\",1,B+,B+\n12,1192,PSY,100,163486,\"Carol,Carol\",1,A,A\n13,1192,MTH,123,163486,\"Carol,Carol\",1,A,A\n14,1199,MTH,231,163486,\"Carol,Carol\",1,A,A\n15,1202,MTH,232,163486,\"Carol,Carol\",1,W,W\n\"\"\""],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["s11 = CSV.read(IOBuffer(s11_data), DataFrame)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Some things are non-essential: `Grade.In` is technical, `Column1` an artifact of writing to a CSV file, ...\n","metadata":{}},
{"cell_type":"markdown","source":"### Access patterns\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Values in DataFrames can be accessed by index, column name, etc.\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["s11[2,5], s11[2, :ID], s11[2, \"ID\"], s11[2, r\"^I\"]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The last one prints differently, as the **column selector** could *possibly* match 0-1 or more columns, so a data frame is returned. The others match just a column, so the value is returned.\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» What \"Term\" is recorded in the 5th row?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"All rows (or all columns) are implied by a colon, `:`:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["s = first(s11, 3)  # first 3 rows\ns.ID, s[:, 5], s[:, \"ID\"], s[:, :ID]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The use of `:` above to reference all rows has an alternative:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["s[!,5], s[!, :ID]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The basic difference is that `:` makes a copy, whereas `!` makes a view. See this [blog post](https://bkamins.github.io/julialang/2022/10/28/indexing.html) for more detail.\n","metadata":{}},
{"cell_type":"markdown","source":"When assigning a value in a column, the use of `s.ID` is convenient, as it replaces the current column.\n","metadata":{}},
{"cell_type":"markdown","source":"## Combining data frames\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Next we define some mock new data for the current semester\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f22_data = \"\"\"\nTerm,Session,Career,Class Nbr,Section,Subject,Catalog,Component,ID,Name,Gender,Phone,Email,Grade,Grade In,Repeat,Instructor ID,Instructor Name,Day,Mtg Start,Mtg End,Add Dt,User,Grade Base\n1229,1,UGRD,36923,D001,MTH,105,LEC,163486,\"Carol,Carol\",F,555-1212,carol.carol@euphoria.edu,,,,24007235,Frank,M,10:10AM,12:05PM,8/17/22,23247055,GRD\n1229,1,UGRD,36923,D001,MTH,105,LEC,163486,\"Carol,Carol\",F,555-1212,carol.carol@euphoria.edu,,,,24007235,Frank,W,10:10AM,12:05PM,8/17/22,23247055,GRD\n1229,1,UGRD,34534,D001,ENG,110,LEC,163486,\"Carol,Carol\",F,555-1212,carol.carol@euphoria.edu,,,,43993434,Faythe,W,10:10:00AM,11:00AM,8/17/22,23247055,GRD\n1229,1,UGRD,43244,D011,PSY,100,LEC,185109,\"Brock,Erin\",U,555-2121,Brock.erin@euphoria.edu,,,,5435352,Grace,Th,2:30PM,4:20PM,,,\n1229,1,UGRD,36923,D001,MTH,105,LEC,185109,\"Brock,Erin\",U,555-2121,Brock.erin@euphoria.edu,,,,24007235,Frank,M,4:40PM,6:10PM,,,\n1229,1,UGRD,44332,D200,ENG,111,LEC,659056,\"Mallory,Yves\",M,555-2211,mallory.eve@euphoria.edu,,,,75544555,Mike,W,8:00AM,9:50AM,,,\n\"\"\""],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"This is read in as before:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["f22 = CSV.read(IOBuffer(f22_data), DataFrame)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Over time the column names evolve. The old data has a minimal set, the new has more extensive repeated data:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["names(s11)"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["names(f22)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"This finds common column names using a Unicode infix operator for `intersect`:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["nms = names(f22) âˆ© names(s11)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» The nms vector is a valid column selector. What is the data frame f22 with only the names from `nms`?\n"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Wrap your previous command within `unique`. What is the difference?\n"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"(The `f22` data is arranged to have replicated data for each day a class meets.)\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"The `vcat` function combines objects vertically (there is also `hcat` and `hvcat`).\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["studs = vcat(s11[:,nms], unique(f22[:,nms]))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"DataFrames has much functionality for other types of data joins\n","metadata":{}},
{"cell_type":"markdown","source":"## Split-apply-combine\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"The [split-apply-combine](https://vita.had.co.nz/papers/plyr.pdf) strategy is often used, and here we see `DataFrames` supports it fairly naturally.\n","metadata":{}},
{"cell_type":"markdown","source":"For this data, we want to create a new data structure for each student:\n","metadata":{}},
{"cell_type":"markdown","source":"containing their 1) first semester, 2) their last semester, and 3) their gpa.\n","metadata":{}},
{"cell_type":"markdown","source":"The first and last semester is conveniently returned by `extrema` when applied to `Term`, given the manner in which the term is coded.\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» What does extrema find for studs.Term?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"The `gpa` requires turning letter grades into numbers. Here is a simple way:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function grade_to_number(x)\n\tismissing(x) && return x\n    x == \"A\"  ? 4.0 :\n\tx == \"A-\" ? 3.7 :\n    x == \"B+\" ? 3.3 :\n\tx == \"B\"  ? 3.0 :\n    x == \"B-\" ? 2.7 :\n    x == \"C+\" ? 2.3 :\n\tx == \"C\"  ? 2.0 :\n    x == \"D\"  ? 1.0 :\n\tx == \"F\"  ? 0.0 : missing\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The `gpa`  would just be done by applying `mean` (from the `Statistics` package).\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["using Statistics  # base Statistics module is very minimal, but has `mean`"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’»  what goes wrong here? A one character fix is?\nxs = grade_to_number(studs.Grade)"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» After ensuring xs is a vector, try finding the mean. What value do you get?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"For `mean(xs)` we have to be a bit careful with\n","metadata":{}},
{"cell_type":"markdown","source":"  * `missing` values (`ismissing`, `skipmissing`)\n  * and empty iterators (`isempty`)\n","metadata":{}},
{"cell_type":"markdown","source":"with this data:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["\nfunction gpa(xs)\n    isempty(xs) && return missing\n    ys = grade_to_number.(xs)\n    all(ismissing.(ys)) && return missing\n    ys |> skipmissing |> mean\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The function we apply to the dataframe for each unique student would be:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function summarize_student(u)\n    m,n = size(u)\n    fterm, lterm = extrema(u.Term)\n    (F=fterm, L=lterm, N = m, gpa = gpa(u.Grade))\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Apply `summarize_student` to the data frame for Alice created by subsetting the rows:\ndf = studs[studs.Name .== \"Abernathy,Alice\",:]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"We can use the `groupby` function to split the data frame on an ID, call the above on each student, and then combine into a data frame.\n","metadata":{}},
{"cell_type":"markdown","source":"The `groupby` function splits the data:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["students = groupby(studs, :ID)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Group the data by the student name"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"The `GroupedDataFrame` object can be iterated over (but not broadcast over). Here we apply our function to each entry:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["student_summaries = [summarize_student(student) for student âˆˆ students]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The `DataFrame` constructor can consume an array of named tuples that is produced by the comprehension, treating each tuple as a new row:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["d = DataFrame(student_summaries)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"### DataFrames mini language\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"DataFrames provides a minilanguage to support the actions:\n","metadata":{}},
{"cell_type":"markdown","source":"  * `combine`: create a new data frame with columns coming from transformations\n  * `select`: create a new data frame with same number of rows (cases) with only the specified columns\n  * `transform`: create a new data frame, as `select` with the same number of cases, but keeps original columns and any additional ones\n","metadata":{}},
{"cell_type":"markdown","source":"Transformations apply a function to source rows and store the result(s) in destination rows. The `=>` pair notation is used to separate. The pattern is\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# source column(s) specifier => function => destination column(s) specifier"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The middle one is a function, which may be an anonymous function, in which case parentheses may be needed due to operator precedence.)\n","metadata":{}},
{"cell_type":"markdown","source":"For example, in the below we will see `:Term => minimum => :F` which will apply the `minimum` function to each `Term` value in a data frame. The `minimum` function is a *reduction* returning a scalar, this will be stored in the computed data frame with variable name `F`. Similarly we have `:L` and `:N` computed:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["students = groupby(studs, :ID)\ncombine(students,\n        :Term => minimum => :F,\n        :Term => maximum => :L,\n        :Term => length => :N,\n        :Grade => gpa => :gpa)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» group studs by :Term then apply `gpa`. Which term has the lowest gpa in the mock data set?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"## Transforming data examples\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"We continue with a larger set of randomly generated mock data. Here we read the data from an internet source, so first the built-in `download` function is called to download the file,\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["url = \"https://raw.githubusercontent.com/jverzani/DataCampPresentation.jl/main/d.csv\"\nd = CSV.read(download(url), DataFrame)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"### Filtering\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"We want to consider the more recent students only, so we filter out the students who started earlier:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["d = filter(r -> r.F >= 1159, d)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"  * Somewhat idiosyncratically `filter` for a data frame filters over rows. (a preferred direction isn't obvious)\n  * the call above is a bit wasteful, as we can filter in place with `filter!`. (The above allocates a new data frame)\n  * As an alternative to `filter` there is `subset` (and `subset!`) which could also be used. E.g.:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["subset(d, :F => ByRow(>=(1209)))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"As another alternate, the mini language can also be used with filter\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["g1209(x) = x >= 1209\nfilter(:F => g1209, d)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"As an covenient alternate, we also have:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["filter(:F => >=(1209), d)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"To explain a bit:\n","metadata":{}},
{"cell_type":"markdown","source":"  * `>=(1209)` is a curried from of `>=(x,y)` with `y=1209` â€“ there are a few such operators for convenience with such tasks;\n  * `subset` needs `ByRow` (to ensure the function consumes an element in the column, not the entire column), whereas `filter` does not, as `filter` returns rows that match the function, so pass rows to the function, whereas, `subset` passes the whole column.)\n  * The `r -> r.F == 1209` anonymous function is probably clearer...\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Using `filter` extract those students whose first term was `1229`. How many were there?"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Can you answer the same question for all terms using `combine`, say?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"### Creating new columns\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"We want to compute how many semesters a student stayed. The data is computable as we have the first and last (`.F` and `.L`) semesters recorded. However, the semester uses an idiosyncratic storage (a leading `1`, two digit year, semester with spring=`1`, fall=`9`.)\n","metadata":{}},
{"cell_type":"markdown","source":"Here we decode:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function decode_semester(x)\n    yr  = div(x - 1000, 10)   # 1229 -> 22\n    val = rem(x, 10) == 2 ? 0.0 : 0.5 # 1229 -> 22 + 0.5; 1222 -> 22 + 0.0\n    yr + val\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"We want to combine the `:F` and `:L` columns and make a new column.  For use, this becomes\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["Î”(f,l) = decode_semester(l) - decode_semester(f) + 1/2\ntransform!(d, [:F, :L] => ByRow(Î”) => :semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The subtlety above is the `ByRow` which is needed to broadcast the values here. In this example, we could have written `Î”` to broadcast with either:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["Î”(f,l) = decode_semester.(l) - decode_semester.(f) .+ 1/2"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Or using the `@.` **macro**:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["@. Î”(f,l) = decode_semester(l) - decode_semester(f) + 1/2"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Then we could have computed with:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["d = transform(d, [:F, :L] => Î” => :alt_semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Can you compute the average number of classes taken per semester for each student?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"### Counting\n","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"The number of semesters a student stays is of interest. At Euphoria State there are many good  reasons to transfer, so the simple model of 8 semesters and out is not typical.\n","metadata":{}},
{"cell_type":"markdown","source":"A simple tally could be done as follows:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["sems = unique(d.semesters)\ncnt = Dict(s => 0 for s âˆˆ sems)  # initialize with a generator\nfor r âˆˆ eachrow(d)\n    cnt[r.semesters] += 1\nend\ncnt"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Since `for` loops are fast in `Julia` this is actually performant, but the dictionary used for counting is not that convenient.\n","metadata":{}},
{"cell_type":"markdown","source":"This counting can be achieved with `combine` followed by sorting:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["df = combine(groupby(d, :semesters), nrow => :N)\nsort(df, :semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» What is the distribution of the number of courses a student took while at Euphoria State?\n# use the argument `rev=true` to sort to order the values. What is the most common number of courses?"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» what is the distribution of the mean number of courses taken by first term (:F)?\n# After sorting, which term had the most?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"## Contigency tables\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"We are curious how the number of semesters has varied over the years and want a contingency table.\n","metadata":{}},
{"cell_type":"markdown","source":"Grouping by more than one column is possible, as this shows:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["df = combine(groupby(d, [:semesters, :F]), nrow => :N)\nsort(df, [:F, :semesters])"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"But a more familiar display is in the form of a contingency table. For that we reach for a package that is useful for contingency tables:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# Pkg.add(\"FreqTables\")\nimport FreqTables: freqtable"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The `freqtable` function is used like `R`'s `table` function (not `xtabs`, with its modeling formula interface):\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["m = freqtable(d.F, d.semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Students on the lower diagonal are still enrolled, other students have left.\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Make a contingency table of :F versus :L for d. Is the shape expected?"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"There are big variations between students who started in the fall versus the spring (more students start in the fall semester at Euphoria State). Here we select fall cohorts:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["d1 = filter(r -> r.F % 10 == 9, d)\nm = freqtable(d1.F, d1.semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Repeat the above, finding a contingency table for those starting in the spring semester (Term ends in 2)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"This pattern of repeated data transformation is often done with a piping syntax, which can feel more natural. Here is one way to do so:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["d |>\n    x -> filter(r -> r.F % 10 == 9, x) |>\n    x -> freqtable(x.F, x.semesters)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"The anonymous functions are easy to write, but difficult to parse. Plus they add some boilerplate that would be nice to remove.\n","metadata":{}},
{"cell_type":"markdown","source":"In `Julia` there are **too** many solutions to this through add-on packages. One is to create a placeholder for the previous value so it can thread through the other might be to create simplified syntax for anonymous functions. We use the `Chain` package and `@chain` macro for the former, the `Underscores` package can do the latter (there are also related `Pipe`, `DataPipes`, `Lazy`, ... packages).\n","metadata":{}},
{"cell_type":"markdown","source":"We will use `Chain`\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["#Pkg.add(\"Chain\")\nusing Chain"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"With `Chain` we use two simple rules:\n","metadata":{}},
{"cell_type":"markdown","source":"  * we can use an underscore, `_`, to specify where the passed along value should fit into the next function call\n  * if no `_` is used, it is passed to the first position.\n","metadata":{}},
{"cell_type":"markdown","source":"Also with `Chain` the piping notation is implicit through a new line.\n","metadata":{}},
{"cell_type":"markdown","source":"The above becomes:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["@chain d begin\n    filter(r -> r.F % 10 == 9, _)\n    freqtable(_.F, _.semesters)\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"(The `Underscores.jl` package could avoid the remaining anonymous function.) Here we define a *closure* to create a function that fixes the semester:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function start_semester(x=:fall)\n    s = x == :spring ? 2 : 9\n    r -> r.F % 10 == s\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Then we have the above can be:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["@chain d begin\n    filter(start_semester(:fall), _)\n    freqtable(_.F, _.semesters)\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Can you filter by fall semester; then filter by :F being 1199 or greater; then make a table of first semster by number of courses?"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» Can you filter by fall semester; then filter by :gpa being 3.0 or greater; then make a table of first semster by number of courses?\n# something like this will be needed: filter(:gpa => !ismissing, _)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"## Visualization\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"A visualization might be helpful. `Julia` has a few add-on packages for making plots: `PyPlot` uses the Python package `Matplotlib`; `GR` uses the GR graphing package; `Plots` is a very useful interface to those backends and others; `Makie` is a powerful package written in `Julia` which shines with 3-d graphics. Here we use the `PlotlyLight` interface to PlotlyJS, as it works quickly under colab.\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# Pkg.add(\"PlotlyLight\")\nusing PlotlyLight"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"`PlotlyLight` is a *lightweight* interface to PlotlyJS, with `Config` used to create `JSON` from `Julia` objects. The PlotlyJS API has some shortcuts to make multiple plots, but for pedagogical reasons we show how to add each at once.\n","metadata":{}},
{"cell_type":"markdown","source":"For this graphic we have to be careful to remove the values on the diagonal, as we are looking for when students leave. First we define a function to make the plotting data (`x`, `y` values and a label) for a given semester.\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function gather_data(s)\n    sem = first(s.F)\n\tm = maximum(s.semesters)\n    n = length(s.semesters)\n\n\txs = 0.5:0.5:m\n\tys = [sum(s.semesters .== i) for i âˆˆ xs] ./ n\n\n    (x = xs[1:end-1], y = ys[1:end-1], name = string(sem))\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"To make different plots with `PlotlyLight`, we set up a basic configuration, and reuse this for each layer:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["cfg = Config(type=\"scatter\", mode=\"lines markers\")\ndata = Config[]  # a typed array with no elements\nfor s  âˆˆ groupby(d, :F)\n    first(s.F) == 1229 && continue\n    plt = copy(cfg)\n    plt.x, plt.y, plt.name = gather_data(s)\n    push!(data, plt)\nend\nlyt = Config(width=800, height=500)\nPlot(data, lyt)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"A similar plot could be formed from the frequency table. In the above, we needlessly recreate that construction in `gather_data` with the comprehension.\n","metadata":{}},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["# ðŸ’» filter out students so only those that started in a fall from fall 19 to fall 22 are shown."],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"---\n","metadata":{}},
{"cell_type":"markdown","source":"Restricting the semesters details a bit more change in the patterns due to the pandemic. We might see an increase in students leaving after an initial semester.\n","metadata":{}},
{"cell_type":"markdown","source":"## More data management\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"Suppose that is to be looked at, we might want to see if the GPA has something to do with it. Perhaps it is lack of preparation due to the pandemic, perhaps not.\n","metadata":{}},
{"cell_type":"markdown","source":"The `gpa` value is numeric, but we would prefer it be categorical. The `cut` function from the `CategoricalArrays` can perform that classification:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["#Pkg.add(\"CategoricalArrays\")\nimport CategoricalArrays: cut"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["rcode(gpa) = cut(gpa, [0.0, 2.0, 3.0, Inf];\n                 labels=[\"lo\", \"medium\", \"hi\"])\ntransform!(d, :gpa => rcode => :status)"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["@chain d begin\n    filter(start_semester(:fall), _)\n    filter(:semesters => ==(0.5), _)\n    freqtable(_.F, _.status)\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Working a bit more, we want to manipulate the frequency table, but our tools are easier with DataFrames. Unfortunately, we don't have the most direct conversion. Here we extract its values and column names for the data frame, then insert the rownames as the first column of our data frame\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["\"\"\"\n    nt_2_df(m::NamedTable; nm=:ID)\n\nConvert named table (e.g., from `FreqTables`) into data frame.\n\"\"\"\nfunction nt_2_df(m; nm=:ID)\n    rnames, cnames = names(m, 1), names(m, 2)\n\tD = DataFrame(m.array, (Symbolâˆ˜string).(cnames))\n\tinsertcols!(D, 1, nm => rnames)\n    D\nend"],"metadata":{},"execution_count":1},
{"outputs":[],"cell_type":"code","source":["_prop(x...) = sum(x[1:end-1]) / x[end]\n\n@chain d begin\n    filter(:F => !=(1229), _)\n    filter(:semesters => ==(0.5), _)\n    freqtable(_.F, _.status)\n    nt_2_df\n    combine(:ID, Not(:ID) => (+) => :N, :)\n    combine(:ID, :N,\n            [:lo, :missing, :N] => ByRow(_prop) => :lo,\n            [:medium, :N] => ByRow(_prop) => :medium,\n            [:hi, :N] => ByRow(_prop) => :hi)\nend\n"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"## Tangent: overriding a base method\n","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"This shoehorns in an example of defining a user defined structure and custom `show` method, a common, easy-to-do, practice.\n","metadata":{}},
{"cell_type":"markdown","source":"This example is to create an alternate display for the table we saw previously:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["sems = unique(d.semesters)\ncnt = Dict(s => 0 for s âˆˆ sems)  # initialize with a generator\nfor r âˆˆ eachrow(d)\n    cnt[r.semesters] += 1\nend\ncnt"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Defining new types is as easy as calling `struct` appropriately:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["struct PrisonCount\n    x::Int\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"This creates an immutable struct, mutable structs are also possible.\n","metadata":{}},
{"cell_type":"markdown","source":"We use the following Unicode string for the display:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["tallies =  \"\\u007C\"^4*\"\\u0338 \""],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"Unicode is more commonly entered using LaTeX shortcuts (e.g., `\\alpha[tab]`), the above uses code points. It also illustrates that `^` for strings is repetition and `*` is concatenation.\n","metadata":{}},
{"cell_type":"markdown","source":"To override the base `show` method for our new type, the method must be imported or qualified, as below, and the acceptable types of `x` below must be narrowed:\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["function Base.show(io::IO, x::PrisonCount)\n    d,r = divrem(x.x, 5)\n    if d > 10\n       print(io, \"($d*5)... + \")\n       d = mod(d, 10)\n    end\n    print(io, tallies^d)\n    println(io, tallies[1:r]) # add newline at end\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"  * `Julia` permits a user to override base types for any type, but the common practice is to only do so for types that a package developer ones. \"Type piracy\" can be an issue.\n  * The `show` method defined above is the catch all, there is also the ability to override based on the MIME type of the display. This notebook shows objects differently than the command line.\n  * Indexing into a string is fruitfully done above. The empty range created by `1:r` when `r=0` requires no special case.\n","metadata":{}},
{"cell_type":"markdown","source":"Finally, we want to see the result\n","metadata":{}},
{"outputs":[],"cell_type":"code","source":["for k âˆˆ sort(collect(keys(cnt)))\n    print(k, \" | \")\n    print(PrisonCount(cnt[k]))\nend"],"metadata":{},"execution_count":1}
  ]
}

